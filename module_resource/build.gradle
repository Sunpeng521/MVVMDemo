import com.phone.versionplugin.BuildVersions

plugins {
    id 'org.jetbrains.kotlin.android'
    //ARouter需要用到
    id 'kotlin-kapt'
    id 'com.phone.versionplugin'
}
if (isModule.toBoolean()) {
    //可独立运行的app
    apply plugin: 'com.android.application'
} else {
    //被依赖的library
    apply plugin: 'com.android.library'
}

android {
    compileSdk BuildVersions.compileSdk

    defaultConfig {
        if (isModule.toBoolean()) {
            //可独立运行的app
            applicationId "com.phone.module_resource"
        } else {
            //被依赖的library
        }
        minSdk BuildVersions.minSdk
        targetSdk BuildVersions.targetSdk
        versionCode BuildVersions.versionCode
        versionName BuildVersions.versionName
        multiDexEnabled true
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        javaCompileOptions {
            annotationProcessorOptions {
                arguments = [AROUTER_MODULE_NAME: project.getName()]
            }
        }
        kapt {
            arguments {
                arg("AROUTER_MODULE_NAME", project.getName())
            }
        }
    }

    //android 模块下开启DataBinding
    dataBinding {
        enabled = true
    }

    //开启Java1_8 能够使用lambda表达式
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = "1.8"
    }

    if (isModule.toBoolean()) {
        //可独立运行的app
        signingConfigs {
            release {
                storeFile file('../rx_java2_and_retrofit2_release.keystore')
                storePassword 'Xz&150120'
                keyPassword 'Xz&150120'
                keyAlias 'rx_java2_and_retrofit2'
            }
        }
    } else {
        //被依赖的library
    }

    sourceSets {
        main {
            jniLibs.srcDirs = ['libs']
            if (isModule.toBoolean()) {
                manifest.srcFile 'src/main/java/module/AndroidManifest.xml'
            } else {
                manifest.srcFile 'src/main/java/library/AndroidManifest.xml'
                java {//移除module包下的代码
                    exclude 'module'
                }
            }
        }
    }

    kapt {
        generateStubs = true
    }

    aaptOptions {
        additionalParameters "--no-version-vectors"
    }

    buildTypes {
        release {
            //java代码中引用的定义形式
            buildConfigField "Boolean", "IS_MODULE", "${isModule}"
            buildConfigField "Boolean", "IS_RELEASE", "${isRelease}"
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
        debug {
            //java代码中引用的定义形式
            buildConfigField "Boolean", "IS_MODULE", "${isModule}"
            buildConfigField "Boolean", "IS_RELEASE", "${isRelease}"
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    //给Module 内的资源名增加前缀, 避免资源名冲突
    resourcePrefix "resource_"
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation project(path: ':library_common')
    implementation project(path: ':library_network')
    implementation project(path: ':library_login')
    implementation project(path: ':library_mvvm')
    implementation project(path: ':library_custom_view')

//    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"

    //kotlin databinding
    kapt "com.android.databinding:compiler:$BuildVersions.databinding_version"

    //Android路由（用于不同app之间的跳转）
//    implementation "com.alibaba:arouter-api:$BuildVersions.arouter_api_version"
    kapt "com.alibaba:arouter-compiler:$BuildVersions.arouter_compiler_version"

}